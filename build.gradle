buildscript {
    apply from: 'versions.gradle'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    }
}

allprojects {
    group = "ru.danil42russia.aaa"
    version = "$versions.project"

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    apply plugin: 'kotlin'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"

        implementation group: "commons-cli", name: "commons-cli", version: "$versions.cli"

        implementation group: "org.apache.logging.log4j", name: "log4j-api", version: "$versions.log4j_api"
        implementation group: "org.apache.logging.log4j", name: "log4j-core", version: "$versions.log4j_core"

        implementation group: "org.flywaydb", name: "flyway-core", version: "$versions.flyway_core"

        implementation group: "org.xerial", name: "sqlite-jdbc", version: "$versions.sqlite_jdbc"

        testImplementation "org.spekframework.spek2:spek-dsl-jvm:$versions.spek"
        testImplementation "org.spekframework.spek2:spek-runner-junit5:$versions.spek"

        testImplementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"
        testImplementation "org.jetbrains.kotlin:kotlin-test:$versions.kotlin"
    }

    test {
        useJUnitPlatform {
            includeEngines 'spek2'
        }
    }

    apply plugin: 'jacoco'
    jacoco {
        toolVersion = "$versions.jacoco"
    }

    task codeCoverageReport(type: JacocoReport) {
        executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
        sourceSets sourceSets.main

        reports {
            xml.enabled = true
            xml.destination file("${buildDir}/reports/jacoco/report.xml")
            html.enabled = false
            csv.enabled = false
        }
    }

    codeCoverageReport.dependsOn test
    check.dependsOn codeCoverageReport
}